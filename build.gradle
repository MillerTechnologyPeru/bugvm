buildscript {
    ext {
        bugvm_version = "1.2.4"
        kotlin_version = "1.1.3"
    }
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath("com.bmuschko:gradle-nexus-plugin:2.3.1")
        classpath("io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.7.0")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}")
    }
}

allprojects {

    group = "com.bugvm"
    version = bugvm_version

    apply plugin: "kotlin"

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    javadoc {
        exclude "**"
    }

    repositories {
        mavenLocal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        mavenCentral()
    }

    dependencies {
        compile("org.jetbrains.kotlin:kotlin-stdlib:${kotlin_version}")
    }

}

apply plugin: "maven"
apply plugin: "signing"
apply plugin: "com.bmuschko.nexus"
apply plugin: "io.codearte.nexus-staging"

defaultTasks "install"

task jars {
    dependsOn install, build, clean
    install.mustRunAfter build
    build.mustRunAfter clean
}

task rt {
    dependsOn ":Core:rt:nativeLib"
}

task llvm {
    dependsOn ":Core:llvm:nativeLib"
}

task all  {
    dependsOn jars, rt, llvm
    jars.mustRunAfter llvm
    llvm.mustRunAfter rt
}

archivesBaseName = "bugvm-dist"
description = "BugVM Dist Package (tgz)"

dependencies {
    compile project(":Core:compiler")
    compile project(":Binding:apple")
    compile project(":Core:rt")
}

task tgzTask(type: Tar) {

    extension = "tgz"
    compression = Compression.GZIP

    into ("bugvm-${version}/bin") {
        from ("Core/bin")
    }

    into("bugvm-${version}/lib") {
        from  "Core/compiler/build/libs/bugvm-compiler-${version}.jar" rename { String fileName -> fileName.replace("bugvm-compiler-${version}.jar", "bugvm-compiler.jar") }
        from("Core/rt/build/libs/bugvm-rt-${version}.jar") rename { String fileName -> fileName.replace("bugvm-rt-${version}.jar", "bugvm-rt.jar") }
        from("Binding/apple/build/libs/bugvm-apple-${version}.jar") rename { String fileName -> fileName.replace("bugvm-apple-${version}.jar", "bugvm-apple.jar") }
    }

    into ("bugvm-${version}/lib/vm") {
        from ("Core/rt/vm/binaries")
    }

    into ("bugvm-${version}/modules/licenses/apple") {
        from ("Binding/apple/NOTICE.txt")
        from ("Binding/apple/LICENSE.txt")
    }

    into ("bugvm-${version}/modules/licenses/llvm") {
        from ("Core/llvm/NOTICE.txt")
        from ("Core/llvm/LICENSE.txt")
    }

    into ("bugvm-${version}/modules/licenses/rt") {
        from ("Core/rt/NOTICE.txt")
        from ("Core/rt/LICENSE.txt")
    }

    into ("bugvm-${version}/modules/licenses/compiler") {
        from ("Core/compiler/NOTICE.txt")
        from ("Core/compiler/LICENSE.txt")
    }

    into ("bugvm-${version}/modules/licenses/vm") {
        from ("Core/vm/NOTICE.txt")
        from ("Core/vm/LICENSE.txt")
    }

    outputs.upToDateWhen { false }
}

artifacts {
    archives tgzTask
}

modifyPom {
    project {
        name project.archivesBaseName
        description project.description
        licenses {
            license {
                name "GNU General Public License, version 2"
                url "https://www.gnu.org/licenses/gpl-2.0.html"
                distribution "repo"
            }
        }
        url "https://bugvm.com"

        scm {
            url "https://github.com/bugvm/bugvm"
            connection "scm:git:git://github.com/bugvm/bugvm.git"
            developerConnection "scm:git:ssh://github.com:bugvm/bugvm.git"
        }

        developers {
            developer {
                id "bugvm"
                name "We are the BugVM"
                email "bug@bugvm.com"
                organization "BugVM"
                organizationUrl "https://bugvm.com"
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "4.0.1"
    distributionType = Wrapper.DistributionType.ALL
}

task listJars {
    doLast {
        configurations.runtime.each { File file -> println file.name }
    }
}