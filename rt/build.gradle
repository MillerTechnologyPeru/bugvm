buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:1.2.3"
    }
}

apply plugin: "com.github.johnrengelman.shadow"

archivesBaseName = "bugvm-rt"
description = """BugVM Runtime Library"""

dependencies {
    compile fileTree(include: ["*.jar"], dir: "lib")
}

jar {
    manifest {
        attributes "Implementation-Version" : project.version
    }
    enabled = false
    dependsOn shadowJar
}

shadowJar {
    classifier = null
    relocate "org.h2", "com.bugvm.h2"
    relocate "org.json", "com.bugvm.json"
    relocate "org.jaudiotagger", "com.bugvm.jaudiotagger"
}

sourceSets {
    main {
        resources {
            srcDirs = ["src/main/resources"]
        }
    }
}

task nativeLib(type:Exec) {
    workingDir "vm"
    commandLine "bash", "-c", ". build.sh --clean"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId project.archivesBaseName

            from components.java
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            pom.project {
                name project.archivesBaseName
                description project.description
            }
        }
    }
}